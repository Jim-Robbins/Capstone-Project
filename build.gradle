// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
        'compileSdk': 26,
        'buildTools': '26.0.1',
        'minSdk': 19,
        'targetSdk': 25,

        'supportLibrary': '26.0.1',
        'butterKnife': '8.7.0',
        'dagger': '1.2.5',
        'espresso': '2.2.2',
        'firebase': '11.4.0',
        'firebaseUI': '2.3.0',
        'gson': '2.6.2',
        'glide': '3.7.0',
        'junit': '4.12',
        'timber': '4.5.1',
    ]

    ext.deps = [
            'support': [
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    'design': "com.android.support:design:${versions.supportLibrary}",
                    'cardView': "com.android.support:cardview-v7:${versions.supportLibrary}",
                    'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    'v4': "com.android.support:support-v4:${versions.supportLibrary}",
                    'constraintLayout': "com.android.support.constraint:constraint-layout:1.0.2",
            ],
            'butterKnife': [
                    'compiler': "com.jakewharton:butterknife-compiler:${versions.butterKnife}",
                    'runtime': "com.jakewharton:butterknife:${versions.butterKnife}",
            ],
            'dagger': [
                    'compiler': "com.squareup.dagger:dagger-compiler:${versions.dagger}",
                    'dagger': "com.squareup.dagger:dagger:${versions.dagger}",
            ],
            'espresso': [
                    'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
                    'contrib': "com.android.support.test.espresso:espresso-contrib:${versions.espresso}",
            ],
            'firebase': [
                    'analytics': "com.google.firebase:firebase-core:${versions.firebase}",
                    'auth': "com.google.firebase:firebase-auth:${versions.firebase}",
                    'database': "com.google.firebase:firebase-database:${versions.firebase}",
                    'ui': "com.firebaseui:firebase-ui:${versions.firebaseUI}",
            ],
            'glide': "com.github.bumptech.glide:glide:${versions.glide}",
            'gson': "com.google.code.gson:gson:${versions.gson}",
            'junit': "junit:junit:${versions.junit}",
            'timber': "com.jakewharton.timber:timber:${versions.timber}",
    ]

    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:2.3.3"
        classpath 'com.google.gms:google-services:3.1.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
